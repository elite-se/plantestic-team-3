modeltype puml "strict" uses puml('http://www.eclipse.plantuml/Puml');
modeltype reqres "strict" uses RequestResponsePairs('http://www.example.org/RequestResponsePairs');

configuration property diagramName : String;

transformation puml2reqres(in input: puml, out output: reqres);

main() {
    log('Started transforming PlantUML to RequestResponse pairs');

     input.objectsOfType(SequenceDiagram).map transformUmlDiagram();
}

mapping SequenceDiagram::transformUmlDiagram(): Scenario {
    scenarioName := diagramName + "_puml";
    var roundtripId : Integer := 1;

    var umlElements : OrderedSet(SequenceElement) := self.umlElements->flattenSequenceElements();
    var umlElementsSize : Integer := umlElements->size();
    var umlElementsIndex : Integer := 1;

    while (umlElementsIndex <= umlElementsSize) {
        var sequenceElement : SequenceElement := umlElements->at(umlElementsIndex);
        if (sequenceElement.oclIsTypeOf(Message)) {
            var message : Message := sequenceElement.oclAsType(Message);
            var request : Request := message.message2request();
            var httpRequestElement : HttpRequest := request.request2httpRequest();
            httpRequestElement.receiver := message.sink.name;

            umlElementsIndex := umlElementsIndex + 1;
            sequenceElement := umlElements->at(umlElementsIndex);

            if (sequenceElement.oclIsTypeOf(Message)) {
                message := sequenceElement.oclAsType(Message);
                var response : Response := message.message2response();
                var httpResponseElement : HttpResponse := response.response2httpResponse();

                var roundtripElement : Roundtrip := generateRoundtrip(roundtripId, httpRequestElement, httpResponseElement);
                roundtrip += roundtripElement;
                roundtripId := roundtripId + 1;
            }
            elif (sequenceElement.oclIsTypeOf(Alternative)) {
                var httpRequestElementCopy : HttpRequest := new HttpRequest();
                httpRequestElementCopy.httpMethod := httpRequestElement.httpMethod;
                httpRequestElementCopy.url := httpRequestElement.url;
                httpRequestElementCopy.httprequestdatum := httpRequestElement.httprequestdatum;
                httpRequestElementCopy.receiver := httpRequestElement.receiver;

                var alternative : Alternative := sequenceElement.oclAsType(Alternative);
                message := alternative.umlElements->at(1).oclAsType(Message);
                var response : Response := message.message2response();
                var httpResponseElement : HttpResponse := response.response2httpResponse();

                var roundtripElement : Roundtrip := generateRoundtrip(roundtripId, httpRequestElementCopy, httpResponseElement);
                roundtripElement.precondition := alternative.text;
                roundtrip += roundtripElement;
                roundtripId := roundtripId + 1;

                var elseElement : Else := alternative.elseBlocks->at(1).oclAsType(Else);
                message := elseElement.umlElements->at(1).oclAsType(Message);
                response := message.message2response();
                httpResponseElement := response.response2httpResponse();

                roundtripElement := generateRoundtrip(roundtripId, httpRequestElement, httpResponseElement);
                roundtripElement.precondition := elseElement.text;
                roundtrip += roundtripElement;
                roundtripId := roundtripId + 1;

            };
        }
        elif (sequenceElement.oclIsTypeOf(Alternative)) {
            var alternative : Alternative := sequenceElement.oclAsType(Alternative);
            var message : Message := alternative.umlElements->at(1).oclAsType(Message);
            var request : Request := message.message2request();
            var httpRequestElement : HttpRequest := request.request2httpRequest();
            httpRequestElement.receiver := message.sink.name;

            message := alternative.umlElements->at(2).oclAsType(Message);
            var response : Response := message.message2response();
            var httpResponseElement : HttpResponse := response.response2httpResponse();

            var roundtripElement : Roundtrip := generateRoundtrip(roundtripId, httpRequestElement, httpResponseElement);
            roundtripElement.precondition := alternative.text;
            roundtrip += roundtripElement;
            roundtripId := roundtripId + 1;
        };
        umlElementsIndex := umlElementsIndex + 1;
    };
}

helper OrderedSet(SequenceElement)::flattenSequenceElements():OrderedSet(SequenceElement) {
    var sequenceElements : OrderedSet(SequenceElement);
    self->forEach(umlElement) {
        if (umlElement.oclIsTypeOf(Message)) {
            var message : Message := umlElement.oclAsType(Message);
            sequenceElements += message;
        }
        elif (umlElement.oclIsTypeOf(Alternative)) {
            var alternative : Alternative := umlElement.oclAsType(Alternative);
            sequenceElements += alternative.map flattenAlternative();
        }
        elif (umlElement.oclIsTypeOf(Activate)) {
            var activate : Activate := umlElement.oclAsType(Activate);
            sequenceElements += activate.umlElements->flattenSequenceElements();
        };
    };
    return sequenceElements;
}

mapping Alternative::flattenAlternative():Alternative {
    umlElements += self.umlElements->flattenSequenceElements();
    elseBlocks += self.elseBlocks->at(1).oclAsType(Else).map flattenElse();
    text := self.text;
}

mapping Else::flattenElse():Else {
    umlElements += self.umlElements->flattenSequenceElements();
    text := self.text;
}

helper Message::message2response():Response {
    assert fatal ( self.content != null )
        with log ( 'Message needs to contain content');
    var requestresponse : RequestResponse := self.content;
    assert fatal ( requestresponse.oclIsTypeOf(Response) )
        with log ( 'Requestresponse needs to be a response');
    return requestresponse.oclAsType(Response);
}

helper Message::message2request():Request {
    assert fatal ( self.content != null )
        with log ( 'Message needs to contain content');
    var requestresponse : RequestResponse := self.content;
    assert fatal ( requestresponse.oclIsTypeOf(Request) )
        with log ( 'Requestresponse needs to be a request');
    return requestresponse.oclAsType(Request);
}

helper generateRoundtrip(roundtripId : Integer, httpRequest : HttpRequest, httpResponse : HttpResponse):Roundtrip {
    var roundtripElement : Roundtrip := new Roundtrip();
    roundtripElement.roundtripName := "roundtrip" + roundtripId.toString();
    roundtripElement.httprequest := httpRequest;
    roundtripElement.httpresponse := httpResponse;
    return roundtripElement;
}

mapping Request::request2httpRequest():HttpRequest {
    httpMethod := self.method;
    url := self.url;
    waitingTime := self.waitingTime;
    httprequestdatum += self.requestParam->map requestParam2httpRequestDatum();
}

mapping Response::response2httpResponse(): HttpResponse {
    postcondition := self.postcondition;
    httpStatus := self.code->toString();
    httpresponsedatum += self.responseParam->map responseParam2httpResponseDatum();
}

mapping RequestParam::requestParam2httpRequestDatum():HttpRequestDatum {
    variableName := self.name;
    value := self.value;
}

mapping ResponseParam::responseParam2httpResponseDatum():HttpResponseDatum {
    variableName := self.name;
    xPath := self.value;
}
